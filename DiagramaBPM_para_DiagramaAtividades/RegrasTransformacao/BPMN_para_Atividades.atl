-- @nsURI MetaModeloUML=http://www.eclipse.org/uml2/3.0.0/UML
-- @nsURI MetaModeloBPMN=http://stp.eclipse.org/bpmn


module BPMNparaAtividades;
create ModeloSaida: MetaModeloUML from ModeloEntrada: MetaModeloBPMN;

--Helper para verificar se a atividade é uma atividade inicial.
helper context MetaModeloBPMN!Activity def: isInitialNode(): Boolean =
	if self.activityType.toString().startsWith('EventStart') and self.activityType <>
			#EventStartTimer and self.activityType <> #EventStartMessage then
		true
	else
		false
	endif;

--Helper para verificar se a atividade é um EventStartTimer.
helper context MetaModeloBPMN!Activity def: isInitialTimerNode(): Boolean =
	if self.activityType = #EventStartTimer then
		true
	else
		false
	endif;


--Helper para verificar se a atividade é um EventStartTimer.
helper context MetaModeloBPMN!Activity def: isInitialMessageNode(): Boolean =
	if self.activityType = #EventStartMessage then
		true
	else
		false
	endif;


--Helper para verificar se a atividade é uma atividade final.
helper context MetaModeloBPMN!Activity def: isFinalNode(): Boolean =
	if self.activityType.toString().startsWith('EventEnd') then
		true
	else
		false
	endif;

--Helper para verificar se source name é null
helper context MetaModeloBPMN!SequenceEdge def: isSourceNameNull(): Boolean =
	if self.source.name.toString().size() = 0 then
		true
	else
		false
	endif;

--Helper para verificar se o target name é null
helper context MetaModeloBPMN!SequenceEdge def: isTargetNameNull(): Boolean =
	if self.target.name.toString().size() = 0 then
		true
	else
		false
	endif;

--Rule para criar uma atividade e modelo UML por cada diagrama BPMN.
rule DiagramaBPMN2ModeloUML {
	from
		ModeloBPMN: MetaModeloBPMN!BpmnDiagram
	to
		ModeloUML: MetaModeloUML!Model (
			name <- 'Modelo UML (gerado)'
		),
		AtividadeUML: MetaModeloUML!Activity (
			name <- 'Atividade (gerada)',
			package <- ModeloUML,
			node <- MetaModeloUML!ActivityNode.allInstances(),
			partition <- MetaModeloUML!ActivityPartition.allInstances(),
			edge <- MetaModeloUML!ActivityEdge.allInstances()
		)
}

-- rules para eventos de inicio
rule InicioBPMN2InicioUML {
	from
		AtividadeInicioBPMN: MetaModeloBPMN!Activity (
			AtividadeInicioBPMN.isInitialNode()
		)
	to
		AtividadeInicioUML: MetaModeloUML!InitialNode (
			name <- AtividadeInicioBPMN.name,
			inPartition <- AtividadeInicioBPMN.lanes.union(AtividadeInicioBPMN.lanes ->
					collect(lane | lane.pool)).union(AtividadeInicioBPMN.groups),
			incoming <- AtividadeInicioBPMN.incomingEdges,
			outgoing <- AtividadeInicioBPMN.outgoingEdges
		)
}

-- rule para event start timer 
rule EventStartTimerBPMN2AcceptEventActionUML {
	from
		AtividadeInicioBPMN: MetaModeloBPMN!Activity (
			AtividadeInicioBPMN.isInitialTimerNode()
		)
	to
		ComentarioUML: MetaModeloUML!Comment (
			body <- AtividadeInicioBPMN.documentation
			),
		AtividadeInicioUML: MetaModeloUML!InitialNode (
			name <- AtividadeInicioBPMN.name,
			inPartition <- AtividadeInicioBPMN.lanes.union(AtividadeInicioBPMN.lanes ->
					collect(lane | lane.pool)).union(AtividadeInicioBPMN.groups),
			incoming <- AtividadeInicioBPMN.incomingEdges,
			outgoing <- AtividadeInicioBPMN.outgoingEdges,
			ownedComment <- ComentarioUML --AtividadeInicioBPMN.documentation --'Frequencia/realizado em: '
		)
}

-- rule para event start message 
rule EventStartMessageBPMN2AcceptEventUML {
	from
		StartMessageBPMN: MetaModeloBPMN!Activity (
			StartMessageBPMN.isInitialMessageNode()
		)
	to
		AcceptEventActionUML: MetaModeloUML!AcceptEventAction (
			name <- StartMessageBPMN.name,
			inPartition <- StartMessageBPMN.lanes.union(StartMessageBPMN.lanes ->
					collect(lane | lane.pool)).union(StartMessageBPMN.groups),
			incoming <- StartMessageBPMN.incomingEdges,
			outgoing <- StartMessageBPMN.outgoingEdges
			
		)		
				
}


-- eventos intermediarios
-- rule para event intermediate message 
rule EventIntermediateMessageBPMN2MensagemUML {
	from
		IntermediateMessageBPMN: MetaModeloBPMN!Activity (
			IntermediateMessageBPMN.activityType = #EventIntermediateMessage
		)
	to
		AcceptEventActionUML: MetaModeloUML!OpaqueAction (
			name <- 'Evento de mensagem: ' + IntermediateMessageBPMN.name,
			inPartition <- IntermediateMessageBPMN.lanes.union(IntermediateMessageBPMN.lanes ->
					collect(lane | lane.pool)).union(IntermediateMessageBPMN.groups),
			incoming <- IntermediateMessageBPMN.incomingEdges,
			outgoing <- IntermediateMessageBPMN.outgoingEdges
			
		)		
				
}





-- rules para eventos de fim
rule FimBPMN2FimUML {
	from
		AtividadeFimBPMN: MetaModeloBPMN!Activity (
			AtividadeFimBPMN.activityType.toString().startsWith('EventEnd')
		)
	to
		AtividadeFimUML: MetaModeloUML!ActivityFinalNode (
			name <- AtividadeFimBPMN.name,
			inPartition <- AtividadeFimBPMN.lanes.union(AtividadeFimBPMN.lanes ->
					collect(lane | lane.pool)).union(AtividadeFimBPMN.groups),
			incoming <- AtividadeFimBPMN.incomingEdges,
			outgoing <- AtividadeFimBPMN.outgoingEdges
		)
}


-- Tasks
rule TaskBPMN2OpaqueAction {
	from
		TaskBPMN: MetaModeloBPMN!Activity (
			TaskBPMN.activityType = #Task and not TaskBPMN.looping
		)
	to
		OpaqueAction: MetaModeloUML!OpaqueAction (
			name <- TaskBPMN.name,
			inPartition <- TaskBPMN.lanes.union(TaskBPMN.lanes -> collect(lane | lane.
					pool)).union(TaskBPMN.groups),
			outgoing <- TaskBPMN.outgoingEdges,
			incoming <- TaskBPMN.incomingEdges
		)
}

rule LoopBPMN2LoopUML {
	from
		LoopBPMN: MetaModeloBPMN!Activity (
			LoopBPMN.activityType = #Task and LoopBPMN.looping
		)
	to
		LoopUML: MetaModeloUML!LoopNode (
			name <- LoopBPMN.name,
			inPartition <- LoopBPMN.lanes.union(LoopBPMN.lanes -> collect(lane | lane.
					pool)).union(LoopBPMN.groups),
			outgoing <- LoopBPMN.outgoingEdges,
			incoming <- LoopBPMN.incomingEdges)
}

-- Gateways
-- Gateway exclusivo
rule ExclusiveGatewayBPMN2DecisionNodeUML {
	from
		ExclusiveGatewayBPMN: MetaModeloBPMN!Activity (
			ExclusiveGatewayBPMN.activityType = #GatewayDataBasedExclusive and 
			ExclusiveGatewayBPMN.outgoingEdges.first() <> ExclusiveGatewayBPMN.
					outgoingEdges.last()
		)
	to
		DecisionNodeUML: MetaModeloUML!DecisionNode (
			name <- if
					ExclusiveGatewayBPMN.name.toString().size() > 0
					then
					ExclusiveGatewayBPMN.name
					else
					'ID:' + ExclusiveGatewayBPMN.iD.toString()
					endif,
			inPartition <- ExclusiveGatewayBPMN.lanes.union(ExclusiveGatewayBPMN.lanes ->
					collect(lane | lane.pool)).union(ExclusiveGatewayBPMN.groups),
			incoming <- ExclusiveGatewayBPMN.incomingEdges,
			outgoing <- ExclusiveGatewayBPMN.outgoingEdges
		)
}

-- Gateway exclusivo
rule ExclusiveGatewayBPMN2MergeNodeUML {
	from
		ExclusiveGatewayBPMN: MetaModeloBPMN!Activity (
			ExclusiveGatewayBPMN.activityType = #GatewayDataBasedExclusive and 
			ExclusiveGatewayBPMN.incomingEdges.first() <> ExclusiveGatewayBPMN.
					incomingEdges.last()
		)
	to
		MergeNodeUML: MetaModeloUML!MergeNode (
			name <- if
					ExclusiveGatewayBPMN.name.toString().size() > 0
					then
					ExclusiveGatewayBPMN.name
					else
					'ID:' + ExclusiveGatewayBPMN.iD.toString()
					endif,
			inPartition <- ExclusiveGatewayBPMN.lanes.union(ExclusiveGatewayBPMN.lanes ->
					collect(lane | lane.pool)).union(ExclusiveGatewayBPMN.groups),
			incoming <- ExclusiveGatewayBPMN.incomingEdges,
			outgoing <- ExclusiveGatewayBPMN.outgoingEdges
		)
}


-- Gateway inclusive
rule InclusiveGatewayBPMN2ForkNodeUML {
	from
		InclusiveGatewayBPMN: MetaModeloBPMN!Activity (
			InclusiveGatewayBPMN.activityType = #GatewayDataBasedInclusive and
					InclusiveGatewayBPMN.outgoingEdges.first() <> InclusiveGatewayBPMN.
					outgoingEdges.last()
		)
	to
		ForkNodeUML: MetaModeloUML!ForkNode (
			name <- if
					InclusiveGatewayBPMN.name <> OclUndefined
					then
					InclusiveGatewayBPMN.name
					else
					'ID:' + InclusiveGatewayBPMN.iD
					endif,
			--name <- 'ID: ' + InclusiveGatewayBPMN.iD,
			inPartition <- InclusiveGatewayBPMN.lanes.union(InclusiveGatewayBPMN.lanes ->
					collect(lane | lane.pool)).union(InclusiveGatewayBPMN.groups),
			incoming <- InclusiveGatewayBPMN.incomingEdges,
			outgoing <- InclusiveGatewayBPMN.outgoingEdges
		)
}

-- Gateway inclusive 
rule InclusiveGatewayBPMN2JoinNodeUML {
	from
		InclusiveGatewayBPMN: MetaModeloBPMN!Activity (
			InclusiveGatewayBPMN.activityType = #GatewayDataBasedInclusive and
					InclusiveGatewayBPMN.incomingEdges.first() <> InclusiveGatewayBPMN.
					incomingEdges.last()
		)
	to
		JoinNodeUML: MetaModeloUML!JoinNode (
			name <- if InclusiveGatewayBPMN.name <> OclUndefined then
					InclusiveGatewayBPMN.name.toString()
				else
					'ID:' + InclusiveGatewayBPMN.iD.toString()
				endif,
			inPartition <- InclusiveGatewayBPMN.lanes.union(InclusiveGatewayBPMN.lanes ->
					collect(lane | lane.pool)).union(InclusiveGatewayBPMN.groups),
			incoming <- InclusiveGatewayBPMN.incomingEdges,
			outgoing <- InclusiveGatewayBPMN.outgoingEdges
		)
}

-- Gateway paralelo
rule ParallelGatewayBPMN2ForkNodeUML {
	from
		ParallelGatewayBPMN: MetaModeloBPMN!Activity (
			ParallelGatewayBPMN.activityType = #GatewayParallel and ParallelGatewayBPMN.
					outgoingEdges.first() <> ParallelGatewayBPMN.outgoingEdges.last()
		)
	to
		ForkNodeUML: MetaModeloUML!ForkNode (
			name <- if ParallelGatewayBPMN.name <> OclUndefined then
					ParallelGatewayBPMN.name.toString()
				else
					'ID:' + ParallelGatewayBPMN.iD.toString()
				endif,
			inPartition <- ParallelGatewayBPMN.lanes.union(ParallelGatewayBPMN.lanes ->
					collect(lane | lane.pool)).union(ParallelGatewayBPMN.groups),
			incoming <- ParallelGatewayBPMN.incomingEdges,
			outgoing <- ParallelGatewayBPMN.outgoingEdges
		)
}

rule ParallelGatewayBPMN2JoinNodeUML {
	from
		ParallelGatewayBPMN: MetaModeloBPMN!Activity (
			ParallelGatewayBPMN.activityType = #GatewayParallel and ParallelGatewayBPMN.
					incomingEdges.first() <> ParallelGatewayBPMN.incomingEdges.last()
		)
	to
		JoinNodeUML: MetaModeloUML!JoinNode (
			name <- if ParallelGatewayBPMN.name <> OclUndefined then
					ParallelGatewayBPMN.name.toString()
				else
					'ID:' + ParallelGatewayBPMN.iD.toString()
				endif,
			inPartition <- ParallelGatewayBPMN.lanes.union(ParallelGatewayBPMN.lanes ->
					collect(lane | lane.pool)).union(ParallelGatewayBPMN.groups),
			incoming <- ParallelGatewayBPMN.incomingEdges,
			outgoing <- ParallelGatewayBPMN.outgoingEdges
		)
}

-- Subprocesso
rule SubProcessBPMN2ExpansionRegionUML {
	from
		SubProcessBPMN: MetaModeloBPMN!Activity (
			SubProcessBPMN.activityType = #SubProcess
		)
	to
		ExpansionRegionUML: MetaModeloUML!ExpansionRegion (
			name <- SubProcessBPMN.name,
			inPartition <- SubProcessBPMN.lanes.union(SubProcessBPMN.lanes ->
					collect(lane | lane.pool)).union(SubProcessBPMN.groups),
			incoming <- SubProcessBPMN.incomingEdges,
			outgoing <- SubProcessBPMN.outgoingEdges
		)
}

-- Rule Data Object para Artefato
rule DataObjectBPMN2DataStoreUML {
	from
		DataObjectBPMN: MetaModeloBPMN!DataObject
	to
		--ObjetoUML: MetaModeloUML!ObjectNode (
		DataStoreUML: MetaModeloUML!DataStoreNode (
			name <- DataObjectBPMN.name,
			incoming <- DataObjectBPMN.associations
		)
}

-- Rule Association para Object Flow
rule AssociationBPMN2ObjectFlowUML {
	from
		AssociationBPMN: MetaModeloBPMN!Association
	to
		ObjectFlowUML: MetaModeloUML!ObjectFlow (
			name <- AssociationBPMN.source.name + '->' + AssociationBPMN.target.name
		)
}

-- Rule Text Annotation para Comment
rule TextBPMN2CommentUML {
	from
		TextBPMN: MetaModeloBPMN!TextAnnotation
	to
		Comment: MetaModeloUML!Comment (
			body <- TextBPMN.documentation
		)
}

-- Rule para transformação de Group  em Partition
rule GroupBPMN2ActivityGroupUML {
	from
		GrupoBPMN: MetaModeloBPMN!Group
	to
		GrupoUML: MetaModeloUML!ActivityPartition (
			name <- GrupoBPMN.name
		)
}

-- rules para transformação das sequence edge BPMN em control flow UML
rule SequenceEdgeBPM2ControlFlowUML {
	from
		SequenceEdgeBPM: MetaModeloBPMN!SequenceEdge
	to
		ActivityEdgeUML: MetaModeloUML!ControlFlow (
			name <- if SequenceEdgeBPM.source.name.toString().size() = 0 or
					SequenceEdgeBPM.source.name = OclUndefined then
					'ID: ' + SequenceEdgeBPM.source.iD + '->' + 
						if SequenceEdgeBPM.target.
								name.toString().size() = 0 or SequenceEdgeBPM.target.name =
								OclUndefined then
							'ID: ' + SequenceEdgeBPM.target.iD.toString()
						else
							SequenceEdgeBPM.target.name.toString()
						endif
					else
						SequenceEdgeBPM.source.name.toString() + '->' + 
							if SequenceEdgeBPM.
								target.name.toString().size() = 0 or SequenceEdgeBPM.target.
								name = OclUndefined then
							'ID: ' + SequenceEdgeBPM.target.iD.toString()
							else
								SequenceEdgeBPM.target.name.toString()
							endif
					endif
		)
}

--Rule para mapear os PoolsBPMN em ActivityPartitions
rule PoolBPM2PartitionUML {
	from
		PoolBPM: MetaModeloBPMN!Pool
	to
		PartitionUML: MetaModeloUML!ActivityPartition (
			name <- PoolBPM.name,
			subpartition <- PoolBPM.lanes
		)
}

--Rule para mapear as LanesBPMN em ActivityPartitions
rule LaneBPM2PartitionUML {
	from
		LaneBPM: MetaModeloBPMN!Lane
	to
		PartitionUML: MetaModeloUML!ActivityPartition (
			name <- LaneBPM.name,
			superPartition <- LaneBPM.pool
		)
}

--Rule para mapear as mensagens BPMN como Control Flow UML
rule MessagingEdgeBPMN2ControlFlowUML {
	from
		MessagingEdgeBPMN: MetaModeloBPMN!MessagingEdge
	to
		ControlFlowUML: MetaModeloUML!ControlFlow (
			name <- if MessagingEdgeBPMN.source.name.toString().size() = 0 or
					MessagingEdgeBPMN.source.name = OclUndefined then
					'ID: ' + MessagingEdgeBPMN.source.iD + '->' + if MessagingEdgeBPMN.
							target.name.toString().size() = 0 or MessagingEdgeBPMN.target.
							name = OclUndefined then
						'ID: ' + MessagingEdgeBPMN.target.iD
					else
						MessagingEdgeBPMN.target.name
					endif
				else
					MessagingEdgeBPMN.source.name.toString() + '->' + if
							MessagingEdgeBPMN.target.name.toString().size() = 0 or
							MessagingEdgeBPMN.target.name = OclUndefined then
						'ID: ' + MessagingEdgeBPMN.target.iD
					else
						MessagingEdgeBPMN.target.name
					endif
				endif,
			source <- MessagingEdgeBPMN.source,
			target <- MessagingEdgeBPMN.target
		)
}



