-- @nsURI MetaModeloUML=http://www.eclipse.org/uml2/3.0.0/UML
-- @nsURI MetaModeloBPMN=http://stp.eclipse.org/bpmn

module Atividades_para_BPMN;
create ModeloSaida:MetaModeloBPMN  from ModeloEntrada: MetaModeloUML;

-- Helpers
--Helper para verificar se a Activity Partition é lane
helper context MetaModeloUML!ActivityPartition def: isLane(): Boolean =
	if 
		self.superPartition <> OclUndefined
	then
		true
	else
		false
	endif;

--Helper para verificar se a Activity Partition é lane
helper context MetaModeloUML!OpaqueAction def: isTask(): Boolean =
	if 
		self.nameExpression = OclUndefined
	then
		true
	else
		false
	endif;


--Helper para verificar se ControlFlow é Sequence Edge
helper context MetaModeloUML!ControlFlow def: isSequenceEdge(): Boolean =
	if 
		self.nameExpression = 'SequenceEdge'
	then
		true
	else
		false
	endif;

--Rules

--Rule para criar uma atividade e modelo UML por cada diagrama BPMN.
rule ModeloUML2DiagramaBPMN {
	from
		AtividadeUML: MetaModeloUML!Activity
		
	to
		ModeloBPMN: MetaModeloBPMN!BpmnDiagram (
			name <- 'Diagrama BPMN (gerado)',
			artifacts <- MetaModeloBPMN!Artifact.allInstances(),
			pools <- MetaModeloBPMN!Pool.allInstances(),
			messages <- MetaModeloBPMN!MessagingEdge.allInstances()
			
		)
}

-- rule que transforma ActivityPartition em Pool
rule PartitionUML2PoolBPMN {
	from
		PartitionUML: MetaModeloUML!ActivityPartition (
			not PartitionUML.isLane()
			)
	to
		PoolBPMN: MetaModeloBPMN!Pool (
			name <- PartitionUML.name,
			lanes <- PartitionUML.subpartition,
			sequenceEdges <- MetaModeloBPMN!SequenceEdge.allInstances()
		
			
			)
}


-- rule que transforma ActivityPartition em Lane
rule PartitionUML2LaneBPMN {
	from
		PartitionUML: MetaModeloUML!ActivityPartition (
			PartitionUML.isLane()
			)
	to
		LaneBPMN: MetaModeloBPMN!Lane (
			name <- PartitionUML.name,
			pool <- PartitionUML.superPartition
			)
}


--Tasks
rule OpaqueActionUML2TaskBPMN {
	from
		OpaqueActionUML: MetaModeloUML!OpaqueAction (
			OpaqueActionUML.isTask()
			)
	to
		ActivityBPMN: MetaModeloBPMN!Activity (
			name <- OpaqueActionUML.name,
			activityType <- 'Task'
			
		)
}


-- Loop Task
rule LoopUML2LoopBPMN {
	from
		LoopUML: MetaModeloUML!LoopNode
	to
		LoopBPMN: MetaModeloBPMN!Activity (
			name <- LoopUML.name,
			activityType <- 'Task',
			looping <- true
		)
}


-- Subprocesso
rule ExpansionRegionUML2SubProcessBPMN {
	from
		ExpansionRegionUML: MetaModeloUML!ExpansionRegion
	to
		SubProcessBPMN: MetaModeloBPMN!Activity (
			name <- ExpansionRegionUML.name,
			activityType <- 'SubProcess'
			)
}

-- Edges
-- Control Flow 2 SequenceEdge
rule ControlFlowUML2SequenceEdgeBPMN {
	from
		ControlFlowUML: MetaModeloUML!ControlFlow (
			ControlFlowUML.isSequenceEdge()	
		)
	to
		SequenceEdgeBPMN: MetaModeloBPMN!SequenceEdge 
}

-- Object Flow 2 Association
rule ObjectFlowUML2AssociationBPMN {
	from
		ObjectFlowUML: MetaModeloUML!ObjectFlow
	to
		AssociationBPMN: MetaModeloBPMN!Association (
			
			)
}

-- Rule para transformar Control Flow em Messaging Edge
rule ControlFlowUML2MessagingEdgeBPMN {
	from
		ControlFlowUML: MetaModeloUML!ControlFlow (
			not ControlFlowUML.isSequenceEdge()
			)
	to
		MessagingEdgeBPMN: MetaModeloBPMN!MessagingEdge
}



--
--Rule para transformar Comment em Text Annotation
rule CommentUML2TextAnnotationBPMN {
	from
		Comment: MetaModeloUML!Comment
	to
		TextBPMN: MetaModeloBPMN!TextAnnotation (
			documentation <- Comment.body	
		)
}


-- Rule de Data Store para Data Object
rule DataStoreUML2DataObjectBPMN {
	from
		DataStoreUML: MetaModeloUML!DataStoreNode
	to
		DataObjectBPMN: MetaModeloBPMN!DataObject (
			name <- DataStoreUML.name,
			associations <- DataStoreUML.incoming
		)
}

-- Eventos
-- rules para eventos de inicio
rule InicioUML2InicioBPMN {
	from 
		InitialNodeUML: MetaModeloUML!InitialNode (
			InitialNodeUML.nameExpression.name = 'EventStartEmpty'
			)
	to
		AtividadeInicioBPMN: MetaModeloBPMN!Activity (
			activityType <- 'EventStartEmpty'
	--		incomingEdges <- InitialNodeUML.incoming,
--			outgoingEdges <- InitialNodeUML.outgoing
			
			)
}

-- rules para eventos de start timer
rule InicioUML2EventStartTimerBPMN {
	from 
		InitialNodeUML: MetaModeloUML!InitialNode (
			InitialNodeUML.nameExpression.name = 'EventStartTimer'
			)
	to
		AtividadeInicioBPMN: MetaModeloBPMN!Activity (
			activityType <- 'EventStartTimer'
			)
}


-- rules para eventos de start message
rule InicioUML2EventStartMessageBPMN {
	from 
		AcceptEventUML: MetaModeloUML!AcceptEventAction
	to
		AtividadeInicioBPMN: MetaModeloBPMN!Activity (
			activityType <- 'EventStartMessage'
			)
}


